package ANALIZADORES.LenguajeD_PP; 
import java_cup.runtime.*;

import java.util.*;
import ESTRUCTURAS.*;

action code {:
    public int Index=1;
    public Nodo Root;

:}


parser code {:
    
    

    public void syntax_error(Symbol s){
        System.err.println("Error en la LÃ­nea " + (s.right+1) +" Columna "+(s.left+1)+ ". Identificador "
        +s.value + " no reconocido." );
        //Inicio.reporteError.agregar("Sintactico",s.right+1,s.left+1,"Identificador "+s.value+" no reconocido");
    }
    
    public Nodo getRoot(){
        return action_obj.Root;
    }

    public Nodo crearNodo(String nombre, token tk)
    {
        Nodo nuevo = new Nodo(action_obj.Index++,nombre,tk.cadena,tk.linea,tk.columna);
        return nuevo;
    }

    public Nodo crearNodo(String nombre, String valor, int linea, int columna)
    {
        Nodo nuevo = new Nodo(action_obj.Index++,nombre,valor,linea,columna);
        return nuevo;
    }
:}


terminal token STRING_LITERAL ,CHAR_LITERAL ,NUM_LITERAL,DECIMAL_LITERAL,ID,MAS,MENOS,POR,DIV,POT,INC,DEC,
APAR,CPAR,ALLA,CLLA,ACORCH,CCORCH,COMA,PYC,DSPTS,IGUAL,IG_IG,MENQ,MENIQ,MAYQ,MAYIQ,DIF,OR,AND,NOT,CADENA,
CARACTER,ENTERO,DECIMAL,BOOLEANO,FALSO,VERDADERO,ESTRUCT,NULO,IMPORTAR,

PTO,SIGNO_Q,SI,SINO,MIENTRAS,PARA,DETENER,
CONTINUAR,RETORNAR,IMPRIMIR,VACIO,PRINCIPAL,
PUNTO, CUADRADO, OVALO, LINEA;

non terminal Nodo inicio,tipo_dato,lista_id,asignacion_var,expl,expr,expa,dato,
                  cuerpo_documento,contenido_cuerpo,declaraciones,declaracion_variable,
                  asignacion,indices,indice,valores_arreglo,una_dimension,
                  lista_valores,declaracion_estructura,cuerpo_estructura,miembros,
                  
                  Acceso,AccesoRecursivo,tipoAcceso,llamada,Lista_ValParams,
                  ValParams,MuchasSentencias,Sentencias,Sentencia, 

                  Sent_Ternario,Sent_Si,final_si,muchos_sino_si,
                  sino_si,sino,Star_SimArr,

                  Sent_Mientras,Sent_Para,Inicializacion,Actualizacion,
                  Simplificada,Sent_Detener,Sent_Continuar,
                  Sent_Retornar,Sent_Imprimir,Metodo,Lista_Parametros,
                  Muchos_SimArr,SimArr,Parametros,
                  Parametro,AccesoArray,MuchosIndices,Pintar_Punto,
                  Pintar_Cuadrado,Pintar_Ovalo,Pintar_Cadena,Pintar_Linea,Funciones_Nativas,importar;


precedence left MAS, MENOS;
precedence left POR, DIV;
precedence left POT;
precedence left APAR,CPAR;
precedence left IG_IG,MENQ,MENIQ,MAYQ,MAYIQ,DIF;

precedence left OR;
precedence left AND;
precedence left NOT;

precedence left SIGNO_Q;

start with inicio;


inicio ::= cuerpo_documento:a {: parser.action_obj.Root = a; :};

//------------------------------------------------------------------------------
cuerpo_documento ::={:  RESULT = parser.crearNodo("cuerpo","",0,0);   :} 
                    |contenido_cuerpo:a
                    {:
                        RESULT = a;
                    :};
//------------------------------------------------------------------------------


//------------------------------------------------------------------------------
contenido_cuerpo ::= contenido_cuerpo:b     declaraciones:a
                    {:
                        b.add(a);
                        RESULT = b;
                    :}
                    |declaraciones:a
                    {:
                        RESULT = parser.crearNodo("cuerpo","",a.linea,a.columna);
                        RESULT.add(a);
                    :};
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
declaraciones   ::= declaracion_variable:a          {: RESULT = a; RESULT.nombre += "Global"; :}
                    |declaracion_estructura:a       {: RESULT = a; :}
                    |asignacion:a                   {: RESULT = a; :}                 
                    |Metodo:a                       {: RESULT = a; :}
                    |Acceso:a PYC                   {: RESULT = a; :}
                    |Simplificada:a PYC             {: RESULT = a; :}
                    |Funciones_Nativas:a PYC        {: RESULT = a; :}
                    |importar:a PYC                 {: RESULT = a; :};
//------------------------------------------------------------------------------



//------------------------------------------------------------------------------
importar       ::= IMPORTAR APAR STRING_LITERAL:a CPAR  {:   RESULT = parser.crearNodo("importar",a.cadena,a.linea,a.columna);  :};
//------------------------------------------------------------------------------


//------------------------------------------------------------------------------
declaracion_estructura ::= ESTRUCT ID:a  ALLA cuerpo_estructura:b CLLA 
                           {:
                                RESULT = parser.crearNodo("declaracion_estructura","",a.linea,a.columna);
                                RESULT.add(parser.crearNodo("id",a.cadena,a.linea,a.columna));
                                RESULT.add(b);
                           :};



cuerpo_estructura      ::= cuerpo_estructura:b miembros:a
                          {: 
                                 b.add(a);
                                 RESULT = b;
                          :}
                          |miembros:a
                          {:
                                RESULT = parser.crearNodo("cuerpo","",a.linea,a.columna);
                                RESULT.add(a); 
                          :};


miembros               ::= declaracion_variable:a       {: RESULT = a; :}
                           |asignacion:a                {: RESULT = a; :}
                           |Simplificada:a PYC          {: RESULT = a; :};


//------------------------------------------------------------------------------
asignacion      ::= Acceso:a IGUAL expl:b PYC 
                    {:
                        RESULT = parser.crearNodo("asignacion","",a.linea,a.columna);
                        RESULT.add(a);
                        RESULT.add(b);
                    :};
//------------------------------------------------------------------------------


//------------------------------------------------------------------------------
declaracion_variable ::= tipo_dato:a lista_id:b MuchosIndices:c asignacion_var:d PYC
                        {:
                            RESULT = parser.crearNodo("primitiva","",a.linea,a.columna);
                            RESULT.add(a);
                            RESULT.add(b);

                            
                            if(!d.nombre.equals(""))
                            {   
                                RESULT.nombre += "DA";
                            }

                            if(!c.hijos.isEmpty())
                            {
                                RESULT.nombre = "arreglo";
                            }
                            
                            RESULT.add(c);
                            RESULT.add(d);
                        :}
                        |ID:a        lista_id:b MuchosIndices:c asignacion_var:d PYC
                        {:
                            RESULT = parser.crearNodo("struct","",a.linea,a.columna);
                            RESULT.add(parser.crearNodo("tipo",a.cadena,a.linea,a.columna));
                            RESULT.add(b);

                            if(!d.nombre.equals(""))
                            {   
                                RESULT.nombre += "DA";
                            }
                            if(!c.hijos.isEmpty())
                            {
                                RESULT.nombre = "arreglo";
                            }
                            RESULT.add(c);
                            RESULT.add(d); 
                        :};
                        
//------------------------------------------------------------------------------


//------------------------------------------------------------------------------
indices ::= indices:b indice:a
           {:
                b.add(a);
                RESULT = b;
           :}
           |indice:a
           {:
                RESULT = parser.crearNodo("indices","",a.linea,a.columna);
                RESULT.add(a);
           :};


indice  ::= ACORCH expl:a CCORCH {:  RESULT = a;  :};
//------------------------------------------------------------------------------




//------------------------------------------------------------------------------
valores_arreglo   ::= una_dimension:a   {:  RESULT = a;  :};


una_dimension     ::= ALLA lista_valores:a CLLA
                      {:
                            RESULT = a;
                      :};

lista_valores     ::= lista_valores:b COMA expl:a
                      {:
                            b.add(a);
                            RESULT = b;
                      :}
                      |expl:a 
                      {:
                            RESULT = parser.crearNodo("dimension_valores","",a.linea,a.columna);
                            RESULT.add(a);
                      :};
//------------------------------------------------------------------------------



//------------------------------------------------------------------------------
tipo_dato ::= ENTERO:a          {: RESULT = parser.crearNodo("tipo",a.cadena,a.linea,a.columna); :} 
            | DECIMAL:a         {: RESULT = parser.crearNodo("tipo",a.cadena,a.linea,a.columna); :} 
            | CARACTER:a        {: RESULT = parser.crearNodo("tipo",a.cadena,a.linea,a.columna); :} 
            | BOOLEANO:a        {: RESULT = parser.crearNodo("tipo",a.cadena,a.linea,a.columna); :} 
            | CADENA:a          {: RESULT = parser.crearNodo("tipo",a.cadena,a.linea,a.columna); :} ;
//------------------------------------------------------------------------------


  
//------------------------------------------------------------------------------
lista_id ::= lista_id:b  COMA ID:a
            {:
                b.add(parser.crearNodo("id",a.cadena,a.linea,a.columna));
                RESULT  =  b;
            :}
            |ID:a
            {:
                RESULT = parser.crearNodo("listaid","",a.linea,a.columna);
                RESULT.add(parser.crearNodo("id",a.cadena,a.linea,a.columna));
            :};
//------------------------------------------------------------------------------



//------------------------------------------------------------------------------
asignacion_var       ::= {::}             {: RESULT = parser.crearNodo("","",0,0); :}
                        |IGUAL expl:a     {: RESULT = a; :};
//------------------------------------------------------------------------------


//---------------------------EXPRESIONES LOGICAS--------------------------------
expl ::= Sent_Ternario:a           {: RESULT = a; :}
        |expl:a AND expl:b         {: RESULT = parser.crearNodo("AND","",a.linea,a.columna);  RESULT.add(a); RESULT.add(b);   :} 
        |expl:a OR expl:b          {: RESULT = parser.crearNodo("OR","",a.linea,a.columna);   RESULT.add(a); RESULT.add(b);   :} 
        |NOT expl:a                {: RESULT = parser.crearNodo("NOT","",a.linea,a.columna);  RESULT.add(a);                  :} 
        |expr:a                    {: RESULT = a; :};
//------------------------------------------------------------------------------

//---------------------------EXPRESIONES RELACIONALES---------------------------
expr ::= expa:a IG_IG expa:b      {: RESULT = parser.crearNodo("IG_IG","",a.linea,a.columna);  RESULT.add(a); RESULT.add(b);   :} 
        |expa:a DIF   expa:b      {: RESULT = parser.crearNodo("DIF","",a.linea,a.columna);    RESULT.add(a); RESULT.add(b);   :} 
        |expa:a MENQ expa:b       {: RESULT = parser.crearNodo("MENQ","",a.linea,a.columna);   RESULT.add(a); RESULT.add(b);   :} 
        |expa:a MENIQ expa:b      {: RESULT = parser.crearNodo("MENIQ","",a.linea,a.columna);  RESULT.add(a); RESULT.add(b);   :} 
        |expa:a MAYQ expa:b       {: RESULT = parser.crearNodo("MAYQ","",a.linea,a.columna);   RESULT.add(a); RESULT.add(b);   :} 
        |expa:a MAYIQ expa:b      {: RESULT = parser.crearNodo("MAYIQ","",a.linea,a.columna);  RESULT.add(a); RESULT.add(b);   :} 
        |expa:a                   {: RESULT = a; :};
//------------------------------------------------------------------------------


//---------------------------EXPRESIONES ARITMETICAS----------------------------
expa ::= expa:a MAS expa:b    {: RESULT = parser.crearNodo("MAS","",a.linea,a.columna);    RESULT.add(a); RESULT.add(b);   :} 
        |expa:a MENOS expa:b  {: RESULT = parser.crearNodo("MENOS","",a.linea,a.columna);  RESULT.add(a); RESULT.add(b);   :} 
        |expa:a POR expa:b    {: RESULT = parser.crearNodo("POR","",a.linea,a.columna);    RESULT.add(a); RESULT.add(b);   :} 
        |expa:a DIV expa:b    {: RESULT = parser.crearNodo("DIV","",a.linea,a.columna);    RESULT.add(a); RESULT.add(b);   :} 
        |expa:a POT expa:b    {: RESULT = parser.crearNodo("POT","",a.linea,a.columna);    RESULT.add(a); RESULT.add(b);   :} 
        |MENOS expa:a         {: RESULT = parser.crearNodo("unario","",a.linea,a.columna); RESULT.add(a); :} 
        |APAR expl:a CPAR     {: RESULT = a; :}
        |dato:a               {: RESULT = a; :}
        |valores_arreglo:a    {: RESULT = a; :}
        |Acceso:a             {: RESULT = a; :};
//------------------------------------------------------------------------------


//------------------------------------------------------------------------------
dato ::= STRING_LITERAL:a  {: a.cadena=a.cadena.substring(1, a.cadena.length()-1); RESULT = parser.crearNodo("STRING_LITERAL",a);    :}
        |CHAR_LITERAL:a    {: RESULT = parser.crearNodo("CHAR_LITERAL",a);    :}
        |NUM_LITERAL:a     {: RESULT = parser.crearNodo("NUM_LITERAL",a);       :}
        |DECIMAL_LITERAL:a {: RESULT = parser.crearNodo("DECIMAL_LITERAL",a);   :}
        |VERDADERO:a       {: RESULT = parser.crearNodo("VERDADERO_LITERAL",a); :}
        |FALSO:a           {: RESULT = parser.crearNodo("FALSO_LITERAL",a);     :}
        |NULO:a            {: RESULT = parser.crearNodo("NULO_LITERAL",a);      :};
//------------------------------------------------------------------------------



//================================Acceso========================================
Acceso      ::= AccesoRecursivo:a {: RESULT = a;  :};


AccesoRecursivo   ::= AccesoRecursivo:b     PTO     tipoAcceso:a
                    {:
                        b.add(a);
                        RESULT = b;
                    :}
                    |tipoAcceso:a
                    {:
                        RESULT = parser.crearNodo("Acceso","",a.linea,a.columna);
                        RESULT.add(a);
                    :};


tipoAcceso        ::= llamada:a     {:  RESULT = a;  :}
                     |AccesoArray:a {:  RESULT = a;  :}
                     |ID:a              
                        {: 
                            RESULT = parser.crearNodo("AccesoId","",a.linea,a.columna); 
                            RESULT.add(parser.crearNodo("id",a.cadena,a.linea,a.columna)); 
                        :};

AccesoArray       ::= ID:a indices:b 
                      {:
                            RESULT = parser.crearNodo("AccesoArray","",a.linea,a.columna);
                            RESULT.add(parser.crearNodo("id",a.cadena,a.linea,a.columna));     
                            RESULT.add(b);
                      :};

//==============================================================================


//================================LLAMADA METODO================================
llamada           ::= ID:a   APAR    Lista_ValParams:b     CPAR     MuchosIndices:c
                      {:
                            RESULT = parser.crearNodo("llamada","",a.linea,a.columna);
                            RESULT.add(parser.crearNodo("id",a.cadena,a.linea,a.columna));
                            RESULT.add(b);
                            RESULT.add(c);
                      :}; 


MuchosIndices     ::=             {: RESULT = parser.crearNodo("indices","",0,0); :}
                    |indices:a    {: RESULT = a; :};
//==============================================================================



//================================Lista Valores Parametros =====================
Lista_ValParams   ::=                 {:    RESULT = parser.crearNodo("ValParams","",0,0);  :}
                      |ValParams:a    {:    RESULT = a;    :};


ValParams         ::= ValParams:b COMA expl:a
                      {:
                            b.add(a);
                            RESULT = b;
                      :}
                      |expl:a
                      {:
                            RESULT = parser.crearNodo("ValParams","",a.linea,a.columna);
                            RESULT.add(a);
                      :};
//==============================================================================






//################################SENTENCIAS CONTROL############################

Sent_Ternario    ::= expl:a SIGNO_Q  expl:b  DSPTS  expl:c 
                    {:
                        RESULT = parser.crearNodo("Ternario","",a.linea,a.columna);
                        RESULT.add(a);
                        RESULT.add(b);
                        RESULT.add(c);
                    :};

//==================================SI==========================================
Sent_Si          ::= SI APAR expl:a CPAR ALLA MuchasSentencias:b CLLA final_si:c 
                     {:
                        RESULT = parser.crearNodo("Si","",a.linea,a.columna);
                        RESULT.add(a);
                        RESULT.add(b);
                        RESULT.add(c);
                     :};

final_si         ::= {:
                        RESULT = parser.crearNodo("partefinal","",0,0);   
                     :}
                     |muchos_sino_si:a sino:b   
                     {:
                        RESULT = parser.crearNodo("partefinal","",a.linea,a.columna);   
                        RESULT.add(a);
                        RESULT.add(b);
                     :}
                     |sino:a                
                     {: 
                        RESULT = parser.crearNodo("partefinal","",a.linea,a.columna);   
                        RESULT.add(a);
                     :};

//------------------------------------------------------------------------------
muchos_sino_si   ::= muchos_sino_si:b sino_si:a
                    {:
                        b.add(a);
                        RESULT = b;
                    :}
                    |sino_si:a
                    {:
                        RESULT = parser.crearNodo("muchos_sinosi","",a.linea,a.columna);
                        RESULT.add(a);
                    :};


sino_si          ::= SINO SI APAR expl:a CPAR ALLA MuchasSentencias:b CLLA
                    {:
                        RESULT = parser.crearNodo("SinoSi","",a.linea,a.columna);
                        RESULT.add(a);
                        RESULT.add(b);
                    :};
//------------------------------------------------------------------------------

sino             ::= SINO:a ALLA MuchasSentencias:b CLLA 
                    {:
                        RESULT = parser.crearNodo("Sino","",a.linea,a.columna);
                        RESULT.add(b);
                    :};
 
//==============================================================================





//==================================MIENTRAS====================================
Sent_Mientras    ::= MIENTRAS APAR  expl:a  CPAR  ALLA MuchasSentencias:b CLLA
                     {:
                            RESULT = parser.crearNodo("Mientras","",a.linea,a.columna);
                            RESULT.add(a);
                            RESULT.add(b);
                     :};
//==============================================================================



//==================================PARA========================================
Sent_Para       ::= PARA APAR  Inicializacion:a PYC expl:b PYC Actualizacion:c  CPAR  ALLA MuchasSentencias:d CLLA
                    {:
                            RESULT = parser.crearNodo("Para","",a.linea,a.columna);
                            RESULT.add(a);
                            RESULT.add(b);
                            RESULT.add(c);
                            RESULT.add(d);
                    :};

Inicializacion  ::= ENTERO:a ID:b  IGUAL expl:c
                    {:
                        RESULT = parser.crearNodo("declaracionPrimitiva","",a.linea,a.columna);
                        RESULT.add(parser.crearNodo("tipo",a.cadena,a.linea,a.columna));

                        Nodo lista_Id = parser.crearNodo("listaid","",b.linea,b.columna);
                        lista_Id.add(parser.crearNodo("id",b.cadena,b.linea,b.columna));

                        RESULT.add(lista_Id);
                        RESULT.add(c);
                    :}
                   |Acceso:a IGUAL expl:b
                   {:
                        RESULT = parser.crearNodo("asignacion","",a.linea,a.columna);
                        RESULT.add(a);
                        RESULT.add(b);
                   :};



Actualizacion   ::= Simplificada:a    {: RESULT = a; :};
//==============================================================================



//================================SIMPLIFICADA==================================
Simplificada    ::= Acceso:a INC  
                   {:
                        RESULT = parser.crearNodo("simplificada","incremento",a.linea,a.columna);
                        RESULT.add(a);
                   :}
                   |Acceso:a DEC
                   {:
                        RESULT = parser.crearNodo("simplificada","decremento",a.linea,a.columna);
                        RESULT.add(a);
                   :};
//==============================================================================




//================================DETENER=======================================
Sent_Detener    ::= DETENER:a          PYC 
                    {:
                        RESULT = parser.crearNodo("detener","",a.linea,a.columna);
                    :};
//==============================================================================



//================================CONTINUAR-====================================
Sent_Continuar    ::= CONTINUAR:a      PYC
                    {:
                        RESULT = parser.crearNodo("continuar","",a.linea,a.columna);
                    :};
//==============================================================================



//================================RETORNAR======================================
Sent_Retornar    ::= RETORNAR:a       PYC
                    {:
                        RESULT = parser.crearNodo("retornar","",a.linea,a.columna);
                    :}
                     |RETORNAR expl:a  PYC
                    {:
                        RESULT = parser.crearNodo("retornar","",a.linea,a.columna);
                        RESULT.add(a);
                    :};
//==============================================================================




//================================IMPRIMIR======================================
Sent_Imprimir    ::= IMPRIMIR APAR expl:a CPAR PYC
                    {:
                        RESULT = parser.crearNodo("imprimir","",a.linea,a.columna);
                        RESULT.add(a);
                    :};
//==============================================================================




//================================MuchasSentencias==============================
MuchasSentencias    ::=                  {:  RESULT = parser.crearNodo("sentencias","",0,0);    :}
                       |Sentencias:a     {:  RESULT = a; :};

Sentencias          ::= Sentencias:b    Sentencia:a
                        {:
                            b.add(a);
                            RESULT = b;
                        :}
                        |Sentencia:a
                        {:
                            RESULT = parser.crearNodo("sentencias","",a.linea,a.columna);
                            RESULT.add(a);
                        :};


Sentencia           ::=  Sent_Si:a                    {:  RESULT = a;  :}
                        |Sent_Mientras:a              {:  RESULT = a;  :}
                        |Sent_Para:a                  {:  RESULT = a;  :}
                        |Sent_Detener:a               {:  RESULT = a;  :}
                        |Sent_Continuar:a             {:  RESULT = a;  :}
                        |Sent_Retornar:a              {:  RESULT = a;  :}
                        |Sent_Imprimir:a              {:  RESULT = a;  :}
                        |asignacion:a                 {:  RESULT = a;  :}
                        |Acceso:a PYC                 {:  RESULT = a;  :}
                        |Simplificada:a PYC           {:  RESULT = a;  :}
                        |declaracion_variable:a       {:  RESULT = a;  :}
                        |Funciones_Nativas:a PYC      {:  RESULT = a;  :};
//==============================================================================



//================================Declaracion Metodos===========================
Metodo          ::= VACIO:a                        ID:b     APAR Lista_Parametros:c CPAR ALLA MuchasSentencias:d CLLA
                    {:
                        RESULT = parser.crearNodo("metodo","",a.linea,a.columna);
                        RESULT.add(parser.crearNodo("tipo","vacio",a.linea,a.columna));
                        RESULT.add(parser.crearNodo("id",b.cadena,b.linea,b.columna));
                        RESULT.add(c);
                        RESULT.add(d);
                        RESULT.add(parser.crearNodo("SimArr","",0,0));
                        
                    :}
                    |tipo_dato:a   Muchos_SimArr:b   ID:c   APAR Lista_Parametros:d CPAR ALLA MuchasSentencias:e CLLA
                    {:
                        RESULT = parser.crearNodo("metodoArray","",a.linea,a.columna);
                        RESULT.add(a);
                        
                        RESULT.add(parser.crearNodo("id",c.cadena,c.linea,c.columna));
                        RESULT.add(d);
                        RESULT.add(e);

                        RESULT.add(b);
                    :}
                    |ID:a          Muchos_SimArr:b   ID:c   APAR Lista_Parametros:d CPAR ALLA MuchasSentencias:e CLLA
                    {:

                        RESULT = parser.crearNodo("metodoArray","",a.linea,a.columna);
                        RESULT.add(parser.crearNodo("tipo",a.cadena,a.linea,a.columna));
                        
                        RESULT.add(parser.crearNodo("id",c.cadena,c.linea,c.columna));
                        RESULT.add(d);
                        RESULT.add(e);

                        RESULT.add(b);

                    :}
                    |tipo_dato:a                   ID:b   APAR Lista_Parametros:c CPAR ALLA MuchasSentencias:d CLLA
                    {:
                        RESULT = parser.crearNodo("metodo","",a.linea,a.columna);
                        RESULT.add(a);
                        RESULT.add(parser.crearNodo("id",b.cadena,b.linea,b.columna));
                        RESULT.add(c);
                        RESULT.add(d);
                        RESULT.add(parser.crearNodo("SimArr","",0,0));
                    :}
                    |ID:a                          ID:b   APAR Lista_Parametros:c CPAR ALLA MuchasSentencias:d CLLA
                    {:
                        RESULT = parser.crearNodo("metodo","",a.linea,a.columna);
                        RESULT.add(parser.crearNodo("tipo",a.cadena,a.linea,a.columna));
                        RESULT.add(parser.crearNodo("id",b.cadena,b.linea,b.columna));
                        RESULT.add(c);
                        RESULT.add(d);
                        RESULT.add(parser.crearNodo("SimArr","",0,0));
                    :}

                    |VACIO:a PRINCIPAL:b APAR CPAR    ALLA MuchasSentencias:c  CLLA
                    {:
                        RESULT = parser.crearNodo("metodo","",a.linea,a.columna);
                        RESULT.add(parser.crearNodo("tipo","vacio",a.linea,a.columna));
                        RESULT.add(parser.crearNodo("id","principal",b.linea,b.columna));
                        RESULT.add(parser.crearNodo("parametros","",0,0));
                        RESULT.add(c);
                        RESULT.add(parser.crearNodo("SimArr","",0,0));
                    :};




Muchos_SimArr        ::= Muchos_SimArr:b    SimArr:a
                       {:
                            b.add(a);
                            RESULT = b;
                       :}
                       |SimArr:a
                       {:
                            RESULT = parser.crearNodo("SimArr","",a.linea,a.columna);
                            RESULT.add(a);
                       :};

SimArr             ::= ACORCH:a CCORCH {:     RESULT = parser.crearNodo("sim","sim",a.linea,a.columna);    :};
//==============================================================================




//================================Lista Parametros =============================
Lista_Parametros   ::= {:  RESULT = parser.crearNodo("parametros","",0,0);  :}
                      |Parametros:a   {:  RESULT = a;  :};

Parametros         ::= Parametros:b COMA Parametro:a
                      {:
                            b.add(a);
                            RESULT = b;
                      :}
                      |Parametro:a
                      {:
                            RESULT = parser.crearNodo("parametros","",a.linea,a.columna);
                            RESULT.add(a);
                      :};


Parametro          ::= tipo_dato:a Star_SimArr:b  ID:c 
                      {:   
                            RESULT = parser.crearNodo("parametroVal","",a.linea,a.columna);
                            RESULT.add(a);
                            RESULT.add(parser.crearNodo("id",c.cadena,c.linea,c.columna));
                            RESULT.add(b);

                      :}
                      |ID:a        Star_SimArr:b  ID:c
                      {:
                            RESULT = parser.crearNodo("parametroStruct","",a.linea,a.columna);
                            RESULT.add(parser.crearNodo("tipo",c.cadena,c.linea,c.columna));
                            RESULT.add(parser.crearNodo("id",c.cadena,c.linea,c.columna));
                            RESULT.add(b);

                      :};

Star_SimArr        ::=  {:  RESULT = parser.crearNodo("SimArr","",0,0); :}
                       |Muchos_SimArr:a
                       {:
                            RESULT = a;
                       :};
//==============================================================================


//================================Funciones NATIVAS=============================


Funciones_Nativas  ::=   Pintar_Cadena:a        {: RESULT = a; :}          
                       | Pintar_Punto:a         {: RESULT = a; :}
                       | Pintar_Cuadrado:a      {: RESULT = a; :}
                       | Pintar_Ovalo:a         {: RESULT = a; :}
                       | Pintar_Linea:a         {: RESULT = a; :};    

Pintar_Punto       ::= PUNTO:a    APAR ValParams:b   CPAR 
                       {:
                            RESULT = parser.crearNodo("pintarpunto","",a.linea,a.columna);
                            RESULT.add(b);
                       :};

Pintar_Cuadrado    ::= CUADRADO:a APAR ValParams:b   CPAR
                        {:
                            RESULT = parser.crearNodo("pintarcuadrado","",a.linea,a.columna);
                            RESULT.add(b);
                       :};

Pintar_Ovalo       ::= OVALO:a    APAR ValParams:b   CPAR
                        {:
                            RESULT = parser.crearNodo("pintarovalo","",a.linea,a.columna);
                            RESULT.add(b);
                       :};

Pintar_Cadena      ::= CADENA:a   APAR ValParams:b   CPAR
                        {:
                            RESULT = parser.crearNodo("pintarcadena","",a.linea,a.columna);
                            RESULT.add(b);
                       :};

Pintar_Linea       ::= LINEA:a    APAR ValParams:b   CPAR
                        {:
                            RESULT = parser.crearNodo("pintarlinea","",a.linea,a.columna);
                            RESULT.add(b);
                       :};
//==============================================================================

